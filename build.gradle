buildscript {
    repositories {
        mavenCentral()
    }
    dependencyLocking {
        lockAllConfigurations()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
}

group = 'de.cronn'
version = '1.3-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(['-Werror'])
}

test {
    useJUnitPlatform()

    maxHeapSize = "256m"

    inputs.dir('data/test/validation')
    outputs.dir('data/test/output')
    outputs.dir('data/test/tmp')
}

dependencies {
    implementation 'org.slf4j:slf4j-api:latest.release'
    implementation 'org.testcontainers:testcontainers:latest.release'
    implementation 'org.apache.commons:commons-lang3:latest.release'

    runtimeOnly 'org.postgresql:postgresql:latest.release'

    testImplementation 'org.junit.jupiter:junit-jupiter-params:latest.release'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:latest.release'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.assertj:assertj-core:latest.release'
    testImplementation 'de.cronn:test-utils:latest.release'
    testImplementation 'de.cronn:validation-file-assertions:latest.release'
    testImplementation 'org.testcontainers:postgresql:latest.release'

    testRuntimeOnly 'ch.qos.logback:logback-classic:latest.release'

    components.all { ComponentMetadataDetails details ->
        if (details.id.version =~ /(?i).+([-.])(CANDIDATE|RC|BETA|ALPHA|M\d+).*/) {
            details.status = 'milestone'
        }
    }
}

dependencyLocking {
    lockAllConfigurations()
}

wrapper {
    gradleVersion = "8.10.1"
    distributionType = Wrapper.DistributionType.ALL
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn test
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            pom {
                name = project.name
                description = 'Snapshot Utilities for PostgreSQL'
                url = 'https://github.com/cronn/postgres-snapshot-util'

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        id = "benedikt.waldvogel"
                        name = "Benedikt Waldvogel"
                        email = "benedikt.waldvogel@cronn.de"
                    }
                    developer {
                        id = "ayk.borstelmann"
                        name = "Ayk Borstelmann"
                        email = "ayk.borstelmann@cronn.de"
                    }
                }

                scm {
                    url = "https://github.com/cronn/postgres-snapshot-util"
                }
            }

            from components.java

            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = project.hasProperty('nexusUsername') ? project.property('nexusUsername') : System.getenv('NEXUS_USERNAME')
                password = project.hasProperty('nexusPassword') ? project.property('nexusPassword') : System.getenv('NEXUS_PASSWORD')
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
